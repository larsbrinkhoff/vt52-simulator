;Converted from listing.

.TITLE VT50 SYMBOLS AND MICRO-PROGRAM

.MACRO .AD A
.NLIST
	.WORD A
.LIST
.ENDM

.MACRO .LD A
.NLIST
	.WORD A!200
.LIST
.ENDM

ZXZY=10
X8=30
IXDY=50
U2M=122
M1=130
M0=170
ZA=110
DY=104
A2M=22
M2A=2
IX=70
IY=64
IA=24
IXDY=50
AEMJ=21
AEM2J=101
ALMJ=41
TRUJ=121
URJ=1
ADXJ=61
DXDY=4
ZX=150
M2B=142
DX=144
DA=164
M2X=102
IROM=124
M2U=42
TABJ=21
B2Y=40
KEYJ=161
VSCJ=141
KCLJ=41
CBFF=60
UTJ=141
ZCAV=100
SCFF=0
SVID=20
XB=10

;Instructions not found on the VT52.
L40M=62		;Load octal 40 into RAM.
eIA2=44		;Same as IA1.
ZY=160		;Presumably to clear the Y register.

;Undefined symbols.
FIN=0	;?
UARTJ=0	;?
EXTRA=0	;?
RING=0	;?
BELLA=0	;?


START:	.AD	UART	;NO-OP AT POWER-UP TIME
	M1!U2M		;CLEAR UAR AND GET IN MODE 1
	ZXZY
PULP:	ZY
	ZA!DY		;GET LAST ROW IN RAM
	A2M		;CLEAR THE LOCATION
	ZA!DY		;GET NEXT ROW
	A2M
	ZA!DY
	IX!L40M!ADXJ	;GET NEXT COLUMN
	.AD	PULP	;DO ANOTHER COLUMN IF NOT FINISHED
CRTN:	ZXZY!DXDY	;CARRIAGE RETURN ROUTIN
	DY!ZA
CRTN2:	DY!ZA		;GET CUR-X LOCATION
CRTN3:	A2M!AEMJ	;CLEAR IT
	.AD	JUMP
TAB:	ZXZY!DXDY	;DO A TAB, MODULO 8
	IXDY
	DXDY!M2A	;GET CUR-X
	.LD	110
	ALMJ		;BEFORE LAST TAB-STOP ?
	.AD	TAB2	;YES-DO AN HONEST TO GOD TAB
	M1!TRUJ		;NO-DO A CURSOR RIGHT INSTEAD
	.AD	CURT2	;OTHERWISE DO CURSOR RIGHT
UART:	X8
	DXDY!M2A!URJ	;IS UART FLAG SET ?
	.AD	CHPROC	;YES GO PROCESS THE NEW CHARACTER
ERASE:	.LD	15	;GET ERASE-COUNTER
	IA2!ALMJ	;FINISHED ?
	.AD	ESS	;NO-GO ERASE ANOTHER LINE
SILSER: IX!IY		;GET LF-BUFFER
	ZA!AEM2J	;IS IT CLEAR ?
	.AD	SIL1	;YES-GO CHECK SILO
	.LD	16
	M2A
	.LD	0	;CLEAR BUFFER
	DY		;GET SILO COUNT
	ALMJ		;SILO OVERFLOW?
	.AD	SCRL2	;YES-DO A SCROLL
	ZXZY!DXDY	;OTHERWISE TRY A LINEFEED
	X8
LINEF:	DXDY!M2A	;GET SCREEN LINE
	.LD	13
	ALMJ		;ON LAST LINE ?
	.AD	CUDOWN	;NO-MOVE CURSOR DOWN
	ZA
	X8!IA!AEMJ	;IS HOLD SCREEN FF SET ?
	.AD	LINEF2
SCRL2:	ZXZY!DXDY
	DY
SCROLL: ZX!M2B		;LOAD TOP LINE IN MABY
	M2A
	ZX!DX		;GET CUR-Y
	A2M		;SET IT WITH TOP LINE
	ZX		;GET TOP-LINE
	.LD	14
	IA2!ALMJ	;INCREMENT IT MODULO 14
	.AD	5$
	ZA
5$:	A2M!AEMJ
	.AD	ERASL	;JUMP TO ERASE LINE ROUTINE
LINEF2: DXDY!M2A	;GET SCROLL COUNT
	DA		;DECREMENT IT
	ALMJ		;ALREADY ZERO ?
	.AD	SCRL1	;NO-SCROLL
BUFF:	ZXZY!DXDY	;SET LF-BUFFER
IDENT:	IX!ADXJ		;SET IDENTIFIER TO TRANSMIT "ESC,\,A"
	.AD	HOLD4
SCRL1:	A2M!AEMJ
	.AD	SCRL2
EOS:	DXDY!M2A	;GET SCREEN LINE
	IX		;GET EOS-COUNTER
	IA!A2M		;SET IT WITH INCREMENTED SCREEN LINE
	ZX!DX!M2B
	M2A
	DXDY		;GET EOS-LINE
	A2M		;SET IT WITH CUR-Y
	ZXZY!DXDY
EOL:	DY
	ZX!DX!M2B	;GET LINE TO BE ERASE
	DY
	M2A		;GET CUR-X
5$:	IX!ADXJ		;DELAY LOOP
	.AD	5$
ERASL:	M1!TRUJ
	.AD	ERASL2
CHPROC: IX
	ZA!AEM2J	;IS SILO SET ?
	.AD	CHPR2
LOADER: M2A		;GET SILO COUNT
LOAD1:	IA!A2M		;INCREMENT IT
	DX
	DXDY!M2A	;GET INPUT POINTER
	IA!A2M		;INCREMENT IT
	M2X
	DXDY		;GET SILO LOCATION
	M1!IROM!U2M!TRUJ ;LOAD CHARACTER IN SILO
	.AD VID
CHPR2:	IY		;GET LF-BUFFER
	ZA!AEM2J	;IS IT SET ?
	.AD	CHPR3	;NO-PROCESS NEW CHARACTER
	.LD	23	
	M2U		;TRANSMIT XOFF
	DY!ZA!AEM2J	;UNCONDITIONAL JUMP
	.AD	LOAD1
CHPR3:	M1!U2M
	IX!IY!ADXJ
	.AD	CHPR4
SIL1:	ZA!DY!AEM2J	;IS SILO SET ?
	.AD	SIL3	;NO-QUIT
	IA2!ALMJ	;IS THIS LAST ITEM ?
	.AD	XONSK	;NO-SKIP XON ROUTINE
	.LD	21	;GET XON CODE
	M2U!ALMJ	;TRANSMIT
	.AD	SIL2
XONSK:	M2A
SIL2:	DA		;DECREMENT SILO COUNT
	A2M
	DXDY!M2A	;GET OUTPUT POINTER
	IA!A2M		;INCREMENT IT
	M2X
CHPR4:	DXDY!M2A	;LOAD CHARACTER
	.LD	0	;CLEAR LOCATION
	ZXZY!DXDY
	IA!XB		;GET SCRATCH LOCATION
	.LD	41	;UPPER LIMIT FOR CONTROL CODES
	ALMJ		;IS IT A CONTROL CODE ?
	.AD	CONTR
	A2M		;STORE CODE
	DX		;GET ESCAPE FF
	ZA!AEM2J	;SET ?
	.AD	DISP	;NO-DISPLAY THIS CHARACTER
	A2M		;CLEAR ESCAPE FF
	IX
	M2A		;GET ASCII CODE
	.LD	102
	AEMJ		;IS IT A ?
	.AD	CURUP
	.LD	111
	AEMJ		;IS IT H ?
	.AD	HOME
	.LD	104
	AEMJ		;IS IT C ?
	.AD	CURRT
	.LD	135
	AEMJ		;IS IT \ ?
	.AD	HOLDIS
	IA!AEMJ		;IS IT [ ?
	.AD	HOLDEN
	IA!AEMJ		;IS IT Z ?
	.AD	IDENT
	.LD	116
	AEMJ		;IS IT K ?
	.AD	EOL
	IA!AEMJ		;IS IT J ?
	.AD	EOS
	M1!IROM!TRUJ
	.AD	VID
CONTR:	.LD	13
	AEMJ		;IS IT LF ?
	.AD	LINEF
	IA!AEMJ		;IS IT TAB ?
	.AD	TAB
	IA!AEMJ		;IS IT BSPACE ?
	.AD	BSPACE
	IA!AEMJ		;IS IT BELL ?
	.AD	BELL
	.LD	37
	AEMJ		;IS IT ESC ?
	.AD	ESCAPE
	.LD	21
	AEMJ		;IS IT CR ?
	.AD	CRTN
	M1!IROM!TRUJ
	.AD	VID	;CAN BE REPLACE BY A "DX"
ESCAPE: DX		;GET ESCAPE FF
ESCA2:	ZA!AEM2J	;IS IT SET ?
	.AD	CURT3	;NO-SET IT
	A2M!AEMJ
	.AD	JUMP	;CLEAR IT
DISP:	IX
	M2A		;GET CHARACTER TO BE DISPLAYED
	DA		;COMPENSATE FOR PREVIOUS INCREMENT
	DY
	ZX!DX!M2B	;GET CUR-Y
	DY!M2X		;GET CUR-X
	B2Y		;GET CURSOR LOCATION
	A2M		;LOAD CHARACTER
CURRT:	ZXZY!DXDY	;CURSOR RIGHT ROUTINE
	IXDY		;GET CUR-X
	DXDY!M2A
CURT2:	.LD	117
	AEMJ		;IN RIGHT MARGIN?
	.AD	JUMP	;EXIT-VT50 DOES NOT WRAP THE CURSOR
CURT3:	IA!A2M!AEMJ	;INCREMENT IT
	.AD	JUMP
HOME:	DXDY		;GET SCREEN LINE
	.LD	0	;CLEAR IT
	ZX		;GET TOP LINE
	IX!IY
SIL3:	DXDY!M2A
	DX
	A2M!AEMJ	;LOAD IT WITH TOP LINE
	.AD	CRTN2
CURUP:	DXDY		;GET SCREEN LINE
	ZA!AEM2J	;IS IT 0 ?
	.AD	JUMP
	M2A
	DA
	A2M!AEMJ	;DECREMENT IT
	.AD	DECR
BSPACE:	DY!ZA
	DY!ZA		;GET CUR-X
	X8!AEMJ		;IN LEFT MARGIN?
	.AD	JUMP	;YES-QUIT
DECR:	ZX!DX
	M2A		;LOAD CUR-X
	DA		;DECREMENT IT
	ALMJ
	.AD	CRTN3
	.LD	13
JUMP:	M1!IROM!TRUJ
	.AD	VID	;JUMP TO NEXT PAGE
HOLDEN:	DXDY
	ZA
	X8!IA!A2M	;SET HOLD SCREEN FF
	X8		;AND CLEAR SCROLL COUNT
HOLDIS:	ZA!DY
BELL:	DX
HOLD4:	X8!A2M!AEMJ
	.AD	JUMP
TAB2:	M0!DA
5$:	IA!TABJ		;THREE LOW ORDER BITS OF AC SET ?
	.AD	5$
	IA!A2M!TABJ
	.AD	JUMP
CUDOWN:	IA!A2M		;INCREMENT SCREEN LINE
	ZX!DX		;GET CUR-Y
	M2A
	.LD	14
	IA2!ALMJ	;INCREMENT IT
	.AD	CRTN3
	M1!TRUJ
	.AD	ESCA2
ESS:	A2M		;INCREMENT EOS-COUNTER
	ZX!DZ		;GET EOS LINE
	DXDY!M2A
	.LD	14
	IA2!ALMJ	;INCREMENT IT
	.AD	5$
	ZA
5$:	A2M
	M2B		;LOAD LINE TO BE ERASED IN MABY
ERASL2:	B2Y		;GET LINE TO BE ERASED
	.LD	117
	M2A		;GET RIGHT MARGIN
	DX
5$:	IX!L40M!ADXJ	;LOOP WHILE ERASING LINE
	.AD	5$
	ZXZY!DXDY!M2A	;GET SCAN COUNT
	IA!A2M
VIDW:	IA!A2M!AEMJ	;INCREMENT IT TWICE
	.AD	VID
SHIFTL:	M0		;GET IN MODE 0
.WORD	252,300,242,337,335,243,245,276,272,241,250,253,251
.WORD	244,336,274,246,277
	TRUJ
	.AD	XMT
TOP2:	DY		;GET TOP LINE
	M2A
	IX!IY		;GET VIDEO LINE
	A2M		;GET THEM EQUAL
KEYBO:	ZXZY!DXDY!M2A	;GET KEYBOARD COUNT
5$:	DA!KEYJ		;IS THE KEY UP ?
	.AD	5$
	A2M		;SAVE ADDRESS
	ZA!AEM2J	;IS IT ZERO?
	.AD	FINISH
	M2A
	DX
	DXDY		;GET BUFFER
	X8!A2M		;STORE IN SCRATCH LOCATION
	IY
	A2M		;STORE IN BOTH SCRATCH LOCATIONS
	X8!AEMJ		;ALREADY TRANSMITTED ?
	.AD	KEYBO
	DY!AEM2J
	.AD	KEYBO
	ZA!AEM2J	;IS THIS BUFFER AVAILABLE ?
	.AD	KEYST
	IY
	ALMJ		;BOTH BUFFERS FULL ?
	.AD	FINISH
KEYST:	X8
	M2A		;GET KEY
	KEYJ		;CHECK KEY AGAIN
	.AD	FINISH
	X8!A2M		;STORE IT
	X8!IA
	.LD	67
	IA!AEMJ
	.AD	PAGE
	M2A
	KEYJ		;IS SHIFT KEY DOWN ?
	.AD	UNSHL	;NO-DO UNSHIFTED LIST
	.LD	42	;LOAD LIMIT FOR SINGLE MEANING KEY
	M2A
UNSHL:	ZXZY!DXDY
	ALMJ		;IS KEY IN SHIFTED RANGE ?
	.AD	SHIFTL	;YET-USE SHIFTED LIST
	M0		;GET IN MODE 0
	ZA
.WORD	331,311,240,303,301,215,233,307,312,302,326,327,332
.WORD	377,211,316,315,306,305,323,330,210,321,324,325,314
.WORD	317,304,320,212,334,310,313,322,270,262,247,255,333
.WORD	263,265,256,273,261,271,275,260,264,266,254,267,257
XMT:	M2U!KCLJ	;TRANSMIT CODE AND CHECK CLICK-JUMPER
	.AD	CLIKSK
	CBFF
CLIKSK:	M1
	.LD	110	;RIGHT MARGIN
	M2A		;GET CUR-X
	DY
	DY!AEM2J
	.AD	BELLA	;RING BELL IF IN RIGHT MARGIN
FINISH:	ZXZY!DXDY
	DX
	ZA!AEM2J	;IS BELL FF SET ?
	.AD	FIN
	M2A
	DA		;DECREMENT BELL COUNT
	A2M!AEMJ
	.AD	RING
PAGE:	ZXZY!DXDY
	CBFF		;SPARE INSTRUCTION
	DXDY
	DXDY
	KEYJ		;IS SHIFT KEY DOWN ?
	.AD	CONTI
	.LD	13
CONTI:	M2A
	IA!A2M!AEMJ	;INCREMENT SCROLL COUNT
	.AD	FINISH
PAINT:	A2M
	DX!M2B		;GET VIDEO LINE
	M2A
	ZX!DX
	DY!AEM2J	;IS THIS THE CUROR LINE?
	.AD	CURSOR	;YES-CHECK BLINKING RATE
FAKE:	ZA
	DA!KEYJ
	.AD PAINT2
CURSOR:	IXDY		;GET FRAME COUNT
	M2A
	IA2!ALMJ
	.AD	FLASH	;TIME TO FLASH CURSOR?
	A2M!AEMJ	;NO-INCREMENT COUNT
	.AD	FAKE
FLASH:	.LD	160
	ZX!DZ
	M2A		;GET CUR-X
PAINT2:	ZXZY!DXDY
	.LD	17	;RESET SCAN COUNT
	X8!A2M		;LOAD CURSOR FLIP-FLOP
	ZXZY
	IXDY		;GET VIDEO LINE
	M2A
	.LD	14
	IA2!ALMJ	;INCREMENT IT
	.AD	PAINT3
	ZA
PAINT3:	A2M
	ZX		;START AT BEGINNING OF LINE
	B2Y
5$:	VSCJ
	.AD	5$	;WAIT FOR END OF SCAN FLAG
	ZXZY!DXDY!M2A
	SVID		;TURN ON VIDEO
	.LD	30
	IA2!ALMJ	;INCREMENT SCAN COUNT AN COMPARE
	.AD	PAINT3
	IA!A2M		;GET PROPER COUNT
	X8
	M2A
	SCFF		;TURN ON CURSOR
	ZX
VID:	VSCJ
	.AD	VID
	ZCAV		;TURN OFF CURSOR AND VIDEO
	ZXZY!DXDY!M2A	;GET SCAN COUNT
	.LD	40
	ALMJ		;TIME FOR ANOTHER UART CHECK ?
	.AD	UARTJ
	.LD	42
	ALMJ		;TIME FOR NEXT LINE ?
	.AD	VIDW	;NO-WAIT A SCAN
	M2X
	M2A		;GET LINE COUNT
	.LD	15
	IA!AEMJ		;CHECK FOR 50/60 HZ COMPENSATION
	.AD	EXTRA
	ALMJ		;MORE LINES TO PAINT ?
	.AD	PAINT
TOPROU: .LD	0
	ZXZY!DXDY
	.LD	66	;GET READY TO SCAN KEYBOARD
	DX
	DXDY!M2A	;GET KEYBOARD BUFFER
	KEYJ		;KEY STILL DOWN
	.AD	TOPROU	;CLEAR IT IF KEY IS UP
	IY		;GET NEXT BUFFER
	M2A
	KEYJ
	.AD	TOPROU
	M0		;GET IN MODE 0
	ZX!UTJ		;IS U-TRANSMITTER READY ?
	.AD	FIN	;NO-SKIP KEYBOARD CHECK
	M1
	ZA!AEM2J	;ALL DONE ?
	.AD	TOP2	;YES-CONTINUE
	M2A
	.LD	57	;CHECK IDENTIFIER
	ALMJ
	.AD	IDE
	.LD	101
	AEMJ
	.AD	TOPROU
	ALMJ
	.AD	IDE
	.LD	33
IDE:	M2U
	M1!TRUJ

.END
